layout-macro-def (timetable-each-time) {
  params {
    param (timeSchedule) {
      type (TimeSchedule)
      min (Required) max (One)
    }
    param (movie) {
      type (Movie)
      min (Required) max (One)
    }
  }
  
  content {
    compound-card {
      on-click {
        intent {
          goal: MakeDetailFromTimeOrderedSchedule
          value: $expr(movie)
          value: $expr(timeSchedule)
        }
      }
      content {
        hbox {
          content {
            vbox {
              halign (End)
              content {
                single-line {
                  text { // 시작 시간
                    style (Title_M)
                    value {
                      template ("#{value(timeSchedule.startTime)}")
                    }
                  }
                }
                single-line {
                  text { // 종료 시간
                    style (Detail_L_Soft)
                    value {
                      template ("~ #{value(timeSchedule.endTime)}")
                    }
                  }
                }
              }
            }
            vbox {
              valign (Middle)
              content {
                single-line {
                  if(timeSchedule.availableSeat * 100 / timeSchedule.totalSeat > 30) {
                    text {
                      color (Green)
                      style (Detail_M)
                      value ("#{value(timeSchedule.availableSeat)}")
                    }
                  }
                  else {
                    text {
                      color (Red)
                      style (Detail_M)
                      value ("#{value(timeSchedule.availableSeat)}")
                    }
                  }
                  text { // totalSeat
                    style (Detail_L)
                    value {
                      template ("/#{value(timeSchedule.totalSeat)}석")
                    }
                  }
                }
                single-line {
                  if (size(timeSchedule.roomPropertyUriList) > 0) {
                    for-each (timeSchedule.roomPropertyUriList) {
                      as (roomProperty) {
                        image { url ("#{value(roomProperty)}") }
                        // spacer
                      }
                    }
                  }
                }
              }
            }
            vbox { // 극장 이름과 거리
              halign (End)
              content {
                single-line { // 극장 이름
                  text {
                    style (Detail_L)
                    value {
                      template ("#{value(timeSchedule.theaterInfo.theaterName)}점")
                    }
                  }
                }
                single-line { // 거리
                  text {
                    style (Detail_L_Soft)
                    value ("#{value(timeSchedule.theaterInfo.distance)}")
                  }
                }
              }
            }
            vbox { // 영화사 이미지 icon
              halign (End)
              valign (Middle)
              content {
                single-line {
                  if(timeSchedule.theaterInfo.brand == 'cgv') {
                    image { url ("images/theater_icon/square/cgv_s.png") style (Title_XL) }
                  }
                  else-if(timeSchedule.theaterInfo.brand == 'megabox') {
                    image { url ("images/theater_icon/square/mega_s.png") style (Title_XL) }
                  }
                  else-if(timeSchedule.theaterInfo.brand == 'lottecinema') {
                    image { url ("images/theater_icon/square/lotte_s.png") style (Title_XL) }
                  }
                }
              }
            }
          }
        }
      }
    }
  }
}