layout-macro-def (timetable-each-time) {
  params {
    param (timeSchedule) {
      type (TimeSchedule)
      min (Required) max (One)
    }
    param (movie) {
      type (Movie)
      min (Required) max (One)
    }
  }
  
  content {
    compound-card {
      on-click {
        intent {
          goal: MakeDetailFromTimeOrderedSchedule
          value: $expr(movie)
          value: $expr(timeSchedule)
        }
      }
      content {
        hbox {
          content {
            vbox {
              grow (1)
              halign (Start)
              valign (Baseline)
              content {
                single-line {
                  text { // 시작 시간
                    style (Title_XS)
                    value {
                      template ("#{value(timeSchedule.startTime)}")
                    }
                  }
                }
              }
            }
            vbox {
              // grow(1)
              valign (Baseline)
              halign (Start)
              content {
                single-line { // 좌석
                  if (timeSchedule.totalSeat == -1) {
                    text { value ("매진") color (Red) style (Detail_L)}
                  }
                  else-if (timeSchedule.totalSeat == -2) {
                    text { value ("마감") style (Detail_L) color (Red) }
                  }
                  else-if (timeSchedule.totalSeat == -3) {
                    text { value ("좌석 자리 준비중") style (Detail_L) color (Amber) }
                  }
                  else {
                    if(timeSchedule.availableSeat * 100 / timeSchedule.totalSeat > 60) {
                      text {
                        color (Green)
                        style (Detail_L)
                        value ("#{value(timeSchedule.availableSeat)}")
                      }
                    }
                    else-if(timeSchedule.availableSeat * 100 / timeSchedule.totalSeat > 30) {
                      text {
                        color (Amber)
                        style (Detail_L)
                        value ("#{value(timeSchedule.availableSeat)}")
                      }
                    }
                    else {
                      text {
                        color (Red)
                        style (Detail_L)
                        value ("#{value(timeSchedule.availableSeat)}")
                      }
                    }
                    text { value ("/#{value(timeSchedule.totalSeat)}석") style (Detail_L) }
                  }
                }
              }
            }
            vbox {
              grow (4)
              halign (End)
              valign (Baseline)
              content {
                single-line {
                  if (timeSchedule.theaterInfo.brand == 'cgv') {
                    text { value ("CGV") style (Detail_L)}
                  }
                  else-if (timeSchedule.theaterInfo.brand == 'megabox') {
                    text { value ("메가박스") style (Detail_L)}

                  }
                  else-if (timeSchedule.theaterInfo.brand == 'lottecinema') {
                    text { value ("롯데시네마") style (Detail_L)}
                  }
                  text { value("#{value(timeSchedule.theaterInfo.theaterName)}점") style (Detail_L)}
                }
              }
            }
          }
        }
        hbox {
          content {
            vbox {
              // grow (1)
              halign (Start)
              valign (Top)
              content {
                single-line { // 종료 시간
                  text { value ("~#{value(timeSchedule.endTime)}") style (Detail_L_Soft) }
                }
              }
            }
            vbox {
              grow (5)
              halign (Start)
              valign (Top)
              content {
                single-line {
                  if (size(timeSchedule.roomPropertyUriList.roomPropertyUri) > 0) {
                    for-each (timeSchedule.roomPropertyUriList.roomPropertyUri) {
                      as (roomProperty) {
                        image { url ("#{value(roomProperty)}") }
                        // spacer
                      }
                    }
                  }
                }
              }
            }
            vbox {
              // grow (1)
              halign (End)
              content {
                single-line { // 거리
                  text { value ("#{value(timeSchedule.theaterInfo.distance)}") style (Detail_L_Soft) }
                }
              }
            }
            // vbox { // 영화사 이미지 icon
            //   halign (End)
            //   valign (Middle)
            //   content {
            //     single-line {
            //       if(timeSchedule.theaterInfo.brand == 'cgv') {
            //         image { url ("images/theater_icon/square/cgv_s.png") style (Title_XL) }
            //       }
            //       else-if(timeSchedule.theaterInfo.brand == 'megabox') {
            //         image { url ("images/theater_icon/square/mega_s.png") style (Title_XL) }
            //       }
            //       else-if(timeSchedule.theaterInfo.brand == 'lottecinema') {
            //         image { url ("images/theater_icon/square/lotte_s.png") style (Title_XL) }
            //       }
            //     }
            //   }
            // }
          }
        }
      }
    }
  }
}