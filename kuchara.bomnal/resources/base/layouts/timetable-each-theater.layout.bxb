layout-macro-def (timetable-each-theater) {
  params {
    param (theaterSchedule) {
      type (TheaterSchedule)
      min (Required) max (Many)
    }
    param (movie) {
      type (Movie)
      min (Required) max (Many)
    }
    param (theaterInfo) {
      type (TheaterInfo)
      min (Required) max (Many)
    }
  }
  content {
    compound-card {
      on-click {
        intent {
          goal: MakeDetailFromTheaterOrderedSchedule
          value: $expr(movie)
          value: $expr(theaterInfo)
          value: $expr(theaterSchedule)
        }
      }
      content {
        hbox {
          content {
            vbox {
              halign (Start)
              valign (Bottom)
              content {
                single-line { // startTime
                  text { value ("#{value(theaterSchedule.startTime)} ") style (Title_XS) }
                  // spacer
                  text { value ("~#{value(theaterSchedule.endTime)}") style (Title_XS) }
                }
              }
            }
            vbox {
              halign (End)
              valign (Bottom)
              content {
                single-line {
                  text { value ("#{value(theaterSchedule.room)}") style (Detail_L) }
                }
              }
            }
          }
        }
        hbox {
          content {
            vbox {
              halign (Start)
              valign (Top)
              content {
                single-line { // 좌석
                  if (theaterSchedule.totalSeat == -1) {
                    text { value ("매진") style (Detail_M) color (Red) }
                  }
                  else-if (theaterSchedule.totalSeat == -2) {
                    text { value ("마감") style (Detail_M) color (Red) }
                  }
                  else-if (theaterSchedule.totalSeat == -3) {
                    text { value ("좌석 자리 준비중") style (Detail_M) color (Amber) }
                  }
                  else {
                    if(theaterSchedule.availableSeat * 100 / theaterSchedule.totalSeat > 60) {
                      text {
                        color (Green)
                        style (Detail_M)
                        value ("#{value(theaterSchedule.availableSeat)}")
                      }
                    }
                    else-if(theaterSchedule.availableSeat * 100 / theaterSchedule.totalSeat > 30) {
                      text {
                        color (Amber)
                        style (Detail_M)
                        value ("#{value(theaterSchedule.availableSeat)}")
                      }
                    }
                    else {
                      text {
                        color (Red)
                        style (Detail_M)
                        value ("#{value(theaterSchedule.availableSeat)}")
                      }
                    }
                    text { value ("/#{value(theaterSchedule.totalSeat)}석") style (Detail_M) }
                  }
                }
              }
            }
            vbox {
              halign (End)
              valign (Top)
              content {
                single-line {
                  if (size(theaterSchedule.roomPropertyUriList.roomPropertyUri) > 0) {
                    for-each (theaterSchedule.roomPropertyUriList.roomPropertyUri) {
                      as (roomProperty) {
                        image { url ("#{value(roomProperty)}") }
                        // spacer
                      }
                    }
                  }
                }
              }
            }
          }
        }
      }
    }
  }
}